#!/usr/bin/perl

use 5.010;
use strict;
use warnings;

use LCD;
use Net::MPD;
use Time::HiRes 'sleep';

my $lcd = LCD->new('/dev/ttyACM0');
my $mpd;

$lcd->clear();
$lcd->contrast(200);
$lcd->brightness(100);
$lcd->color(255, 255, 255);

$SIG{INT} = sub {
  $lcd->clear();
  $lcd->contrast(200);
  $lcd->brightness(0);
  exit;
};

sub error {
  my ($message, $retry) = @_;

  $lcd->color(255, 0, 0);
  $lcd->clear();
  $lcd->say($message);

  if ($retry) {
    for (reverse 1 .. $retry) {
      $lcd->set_cursor(1, 2);
      $lcd->print("Retry in $_ ");
      sleep 1;
    }
  }
}

sub line {
  my ($string) = @_;
  $lcd->print(substr("$string\n", 0, 16));
}

sub show_current {
  $mpd->update_status;
  if ($mpd->state eq 'play') {
    my $song = $mpd->current_song;
    $lcd->color(255, 255, 255);
    $lcd->clear();
    line($song->{Album} || 'Unknown');
    line($song->{Title} || 'Untitled');
  } else {
    error('Not playing');
  }
}

while (1) {
  $mpd = eval { Net::MPD->connect() };
  last if $mpd;
  error('Connection error', 10);
}

while (1) {
  show_current($mpd->current_song);
  $mpd->idle(qw'playlist player');
}


