#!/usr/bin/perl

use 5.010;
use strict;
use warnings;

use LCD;
use Net::MPD;
use Time::HiRes 'sleep';

my $lcd = LCD->new('/dev/lcd');

$lcd->clear();
$lcd->contrast(200);
$lcd->brightness(100);
$lcd->color(255, 255, 255);

local $SIG{TERM} = sub {
  $lcd->clear();
  $lcd->contrast(200);
  $lcd->brightness(0);
  exit;
};

sub line {
  my ($string) = @_;
  $lcd->print(substr("$string\n", 0, 16));
}

sub error {
  my ($message, $retry) = @_;

  $lcd->color(255, 0, 0);
  $lcd->clear();
  line($message);

  if ($retry) {
    for (reverse 1 .. $retry) {
      $lcd->set_cursor(1, 2);
      $lcd->print("Retry in $_ ");
      sleep 1;
    }
  }
}

sub show_current {
  my ($mpd) = @_;

  $mpd->update_status;
  if ($mpd->state eq 'play') {
    my $song = $mpd->current_song;
    $lcd->color(255, 255, 255);
    $lcd->clear();
    line($song->{Album} || 'Unknown');
    line($song->{Title} || 'Untitled');
  } else {
    error('Not playing');
  }
}

$lcd->set_splash('eatabrick radio                 ');

while (1) {
  eval {
    my $mpd = Net::MPD->connect();
    while (1) {
      show_current($mpd);
      $mpd->idle(qw'playlist player');
    }
  };

  error($@, 10);
}


